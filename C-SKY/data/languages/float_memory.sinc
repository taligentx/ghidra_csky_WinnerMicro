with : fp2_op = 0b1101 {
    fp2_disp: val is fp2_imm4_imm_h & fp2_imm4_imm_l [val = ((fp2_imm4_imm_h << 4) + fp2_imm4_imm_l) << 2;] {export val;}

    #flds supercedes fld.32
    :flds        fp2_imm4_vrz (fp2_r_rx, fp2_disp) is fp2_imm4_sop = 0b00100000 & fp2_imm4_vrz & fp2_r_rx & fp2_disp {
        local addr:4 = fp2_r_rx + fp2_disp;
        fp2_imm4_vrz = *:4 addr;
    }
    :fldms       fp2_imm4_vry, "count="^fp2_imm4_imm_h, (fp2_r_rx) is fp2_imm4_sop = 0b00110000 & fp2_imm4_vry & fp2_imm4_imm_h & fp2_r_rx {
        local addr:4 = fp2_r_rx;
        local offset:4 = 0;
        local limit:4 = fp2_imm4_imm_h * 4;

        <start>
        if (offset > limit) goto <end>;
        local val:4 = *:4 addr;
        local reg_ptr:4 = &fp2_imm4_vry + offset;
        *[register]:4 reg_ptr = val;
        addr = addr + 4;
        offset = offset + 4;
        goto <start>;
        <end>
    }
    :fstms       fp2_r_vry, "count="^fp2_imm4_imm_h, (fp2_r_rx) is fp2_imm4_sop = 0b00110100 & fp2_r_vry & fp2_imm4_imm_h & fp2_r_rx {
        local addr:4 = fp2_r_rx;
        local offset:4 = 0;
        local limit:4 = fp2_imm4_imm_h * 4;

        <start>
        if (offset > limit) goto <end>;
        local reg_ptr:4 = &fp2_r_vry + offset;
        local val:4 = *[register]:4 reg_ptr;
        *:4 addr = val;
        addr = addr + 4;
        offset = offset + 4;
        goto <start>;
        <end>
    }
    :fsts        fp2_imm4_vrz, (fp2_r_rx, fp2_disp) is fp2_imm4_sop = 0b00100100 & fp2_imm4_vrz & fp2_r_rx & fp2_disp {
        local addr:4 = fp2_r_rx + fp2_disp;
        *:4 addr = fp2_imm4_vrz;
    }
    :fldrs       fp2_r_vrz, (fp2_r_rx, fp2_r_ry fp2_imm2_imm) is fp2_imm2_sop = 0b00101000 & fp2_r_vrz & fp2_r_rx & fp2_r_ry & fp2_imm2_imm {
        local offset:4 = fp2_r_ry << fp2_imm2_imm;
        local addr:4 = fp2_r_rx + offset;
        fp2_r_vrz = *:4 addr;
    }
    :fstrs fp2_r_vrz, (fp2_r_rx, fp2_r_ry "<<" fp2_imm2_imm) is fp2_imm2_sop = 0b00101100 & fp2_r_vrz & fp2_r_rx & fp2_r_ry & fp2_imm2_imm {
        local offset:4 = fp2_r_ry << fp2_imm2_imm;
        local addr:4 = fp2_r_rx + offset;
        *:4 addr = fp2_r_vrz;
    }
}
